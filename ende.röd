#!/usr/bin/env r√∂da

/** Main program **/

main args... {
	code := ""
	args | for arg do
		if [ arg =~ "-h|-help|--help" ] do
			print "usage: ende { -e <code> | <file> }"
			continue
		done
		if [ arg = "-e" ] do
			code .= pull() .. "\n"
		else
			readLines(arg) | code .= _ .. "\n"
		done
	done
	env := OBJ
	if [ #code > 0 ] do
		parse code | _.evaluate env
	else
		while true do
			push ">> "
			parse(pull()) | _.evaluate env
		done
	done
}

/** Interpreter **/

{
	STACK := []
}

printStack {
	STACK | enum | for elem, i do
		errprint `$i: $elem`.."\n"
	done
}

record Value {
	dict : map = {
		dict := new map
		dict["__proto__"] = OBJ
		return dict
	}()
	
	rec : boolean = FALSE
	
	function has key {
		return FALSE if [ self.rec ]
		self.rec = TRUE
		push self.dict[key]? or (self.dict["__proto__"]? and self.dict["__proto__"].has(key))
		self.rec = FALSE
	}
	
	function get key {
		return NIL if [ self.rec ]
		return self.dict[key] if [ self.dict[key]? ]
		self.rec = TRUE
		self.dict["__proto__"].get key if [ self.dict["__proto__"]? ] else
		{ printStack(); error "Key not found: "..key.."\n"; push NIL }
		self.rec = FALSE
	}
	
	queue : Queue = new Queue
	
	function subenv parent, &vars... {
		if [ #vars = 0 ] do
			subenv := new Value
			subenv.dict["__proto__"] = self
			subenv.dict["__parent__"] = self
			return subenv
		else
			vars | _ := self.subenv(parent)
		done
	}
	
	function end {
		self.queue.pullAll | (self.parent()).queue.push
	}
	
	function pulln n {
		while [ n > 0 ] do
			n --
			self.queue.pull
		done
	}
	
	function push vals... {
		self.queue.push *vals
	}
	
	function parPulln n {
		if self.hasParent do
			(self.parent()).pulln n
		else
			printStack()
			error "attempted to access the parent of the top object"
		done
	}
	
	function parPush vals... {
		if self.hasParent do
			(self.parent()).push *vals
		else
			printStack()
			error "attempted to access the parent of the top object"
		done
	}
	
	function hasParent {
		return self.dict["__parent__"]?
	}
	
	function parent {
		return self.dict["__parent__"]
	}
	
	function toString {
		self.toRepr
	}
	
	function toRepr {
		return "[object "..self.dict["__name__"].toString().."]" if [ self.dict["__name__"]? ]
		return "..." if [ self.rec ]
		self.rec = TRUE
		val := [keys(self.dict) | sort | push `$key: ${self.dict[key].toRepr()}` for key]&", "
		push "{"..val.."; "..self.queueToString().."}"
		self.rec = FALSE
	}
	
	function queueToString {
		if self.hasParent do
			return self.parent().queueToString() .. " / " .. self.queue.toString()
		else
			return self.queue.toString()
		done
	}
	
	function toInteger {
		return 0
	}
	
	function toBoolean {
		if [ self.dict["__bool__"]? ] do
			self.dict["__bool__"].toBoolean
		else
			true
		done
	}
}

record CmdValue(body) : Value {
	function eval cenv {
		cenv.subenv cenv, env2
		body env2 | env2.push
		env2.end
	}
	
	function toString {
		return "<command "..body..">"
	}
}

record StrValue(str) : Value {
	dict : map = {
		dict := new map
		dict["__proto__"] = STR_PROTO
		return dict
	}()
	
	function eval cenv {
		cenv.subenv cenv, env2
		parse(str) | _.evaluate env2
		env2.end
	}
	
	function toString {
		return str
	}
	
	function toRepr {
		return `"$str"`
	}
	
	function toInteger {
		parseInteger str if [ str =~ "-?[0-9]+" ] else return 0
	}
	
	function toBoolean {
		return str != ""
	}
}

record IntValue(int) : Value {
	dict : map = {
		dict := new map
		dict["__proto__"] = INT_PROTO
		return dict
	}()
	
	val : integer = int
	
	function toInteger {
		return self.val
	}
	
	function toString {
		return `${self.val}`
	}
	
	function toRepr {
		return `${self.val}`
	}
	
	function toBoolean {
		return self.val != 0
	}
}

/** Builtins **/

post_load: {
	OBJ := 0
	OBJ := new Value
	del OBJ.dict["__proto__"]

	NIL := new Value
	OBJ := new Value
	
	STR_PROTO := new Value
	INT_PROTO := new Value
	PROMISE_PROTO := new Value
	LIST_PROTO := new Value
	
	OBJ.dict["__name__"] = new StrValue("Object")
	keys BUILTINS | OBJ.dict[_1] = new CmdValue(BUILTINS[_1])
	OBJ.dict["__proto__"] = NIL
	OBJ.dict["__bool__"] = new IntValue(1)
	
	NIL.dict["__name__"] = new StrValue("Nil")
	NIL.dict["__bool__"] = new IntValue(0)
	OBJ.dict["Nil"] = NIL
	
	STR_PROTO.dict["__name__"] = new StrValue("String")
	STR_PROTO.dict["."] = new CmdValue({ |env|; push new StrValue(env.parent().toString()..env.parPulln(1).toString()) })
	OBJ.dict["String"] = STR_PROTO
	
	INT_PROTO.dict["__name__"] = new StrValue("Integer")
	INT_PROTO.dict["+"] = new CmdValue({ |env|; push new IntValue(env.parent().toInteger()+env.parPulln(1).toInteger()) })
	INT_PROTO.dict["-"] = new CmdValue({ |env|; push new IntValue(env.parent().toInteger()-env.parPulln(1).toInteger()) })
	INT_PROTO.dict["*"] = new CmdValue({ |env|; push new IntValue(env.parent().toInteger()*env.parPulln(1).toInteger()) })
	INT_PROTO.dict["/"] = new CmdValue({ |env|; push new IntValue(env.parent().toInteger()/env.parPulln(1).toInteger()) })
	seq 0, 9 | for i do
		INT_PROTO.dict[`$i`] = new CmdValue({ |env|
			obj := env.parent()
			{ printStack(); error "primitive operation on non-primitive object" } unless [ obj is IntValue ]
			obj.val *= 10
			obj.val += i
		})
	done
	OBJ.dict["Integer"] = INT_PROTO
	
	PROMISE_PROTO.dict["__name__"] = new StrValue("Promise")
	["ok", "then", "fail", "catch"] | for init, handle do
		PROMISE_PROTO.dict[init] = new CmdValue({ |env|
			obj := env.parent()
			payload := env.pulln(1)
			if obj.has "_h"..init do
				env2 := obj.get("_e"..init)
				callback := obj.get("_h"..init)
				env2.push payload
				parse(callback.toString()) | _.evaluate env2
			else
				obj.dict["_v"..msg] = payload
			done
		})
		PROMISE_PROTO.dict[handle] = new CmdValue({ |env|
			obj := env.parent()
			env2 := obj.pulln(1)
			callback := obj.pulln(1)
			if obj.has "_v"..init do
				env2.push obj.get("_v"..init)
				parse(callback.toString()) | _.evaluate env2
			else
				obj.dict["_h"..init] = callback
				obj.dict["_e"..init] = env2
			done
		})
	done
	OBJ.dict["Promise"] = PROMISE_PROTO
	
	LIST_PROTO.dict["__name__"] = new StrValue("List")
	LIST_PROTO.dict["f"] = new CmdValue({ |env|
		obj := env.parent()
		env2 := obj.pulln(1)
		callback := obj.pulln(1)
		i := 0
		while obj.has `$i` do
			env2.push obj.get(`$i`)
			parse(callback.toString()) | _.evaluate env2
			i ++
		done
	})
	LIST_PROTO.dict["length"] = new CmdValue({ |env|
		obj := env.parent()
		i := 0
		while obj.has `$i` do
			i++
		done
		obj.push new IntValue(i)
	})
	OBJ.dict["List"] = LIST_PROTO
}

{
	BUILTINS := new map
	
	/* queue */
	BUILTINS["<"] = { |env|; (env.parent()).queue.peek }
	BUILTINS[","] = { |env|; env.parPulln 1 | {} }
	BUILTINS["%"] = { |env|; (env.parent()).parPulln 1 }
	BUILTINS["@"] = { |env|; (env.parent()).parPush env.parent().parPulln(1) }
	
	/* numbers */
	BUILTINS["#"] = { |env|; push new IntValue(0) }
	BUILTINS["range"] = { |env|
		env.subenv env, range
		range.dict["__proto__"] = LIST_PROTO
		env.parPulln 2 | _.toInteger | seq _, _ | enum | range.dict[`$_2`] = new IntValue(_1)
		env.parPush range
	}

	/* eval */
	BUILTINS["^"] = { |env|; env.subenv env, env2; env.parPulln 1 | parse(_.toString()) | _.evaluate env2; env2.end }
	/*BUILTINS["^"] = { |env|; obj := env.parPulln(1); obj.subenv env.parent(), env2; env.parPulln 1 | parse(_.toString()) | _.evaluate env2; env2.end }*/
	BUILTINS[":"] = { |env|; obj := env.parPulln(1); env.parPulln 1 | parse(_.toString()) | _.evaluate obj }
	
	/* objs */
	BUILTINS["$"] = { |env|; env.parent }
	BUILTINS[">"] = { |env|; env.parPulln 2 | obj.push val for obj, val }
	BUILTINS["&"] = { |env|; env.parPulln 1 | _.pulln(1) }
	BUILTINS["?"] = { |env|; env.parPulln 1 | (env.parent()).get(key.toString()) for key }
	BUILTINS["="] = { |env|; env.parPulln 2 | (env.parent()).dict[key.toString()] = val for key, val }
	BUILTINS["!"] = { |env|; env.parPulln 1 | del (env.parent()).dict[_.toString()] }

	/* control flow */
	BUILTINS["i"] = { |env|
		cond := env.parPulln(1)
		env.subenv env, promise
		promise.dict["__proto__"] = PROMISE_PROTO
		if cond.toBoolean do
			promise.dict["_vok"] = cond
		else
			promise.dict["_vfail"] = cond
		done
		env.parPush promise
	}

	/* i/o */
	BUILTINS["d"] = { |env|; env.parPulln 1 | STDOUT.push _.toString() }
	BUILTINS["r"] = { |env|; env.parPulln 1 | STDOUT.push _.toRepr() }
	BUILTINS["q"] = { |env|; env.parPulln 1 | STDOUT.push _.queue.toString() }
	
	BUILTINS["l"] = { |env|
		env.subenv env, promise
		promise.dict["__proto__"] = PROMISE_PROTO
		try do
			promise.dict["_vok"] = new StrValue(STDIN.pull())
		catch e
			promise.dict["_vfail"] = new StrValue(e.message)
		done
		env.parPush promise
	}
}

/** Parser **/

parse code {
	chars code | for char do
		push(new BlockCommand(parseBlock("{", "}"))) if [ char = "{" ] else
		push(new StringCommand(parseBlock("[", "]"))) if [ char = "[" ] else
		push(new CallCommand(parseBlock("(", ")"))) if [ char = "(" ] else
		push(new CallCommand(char)) if [ char =~ `\S` ]
	done
}

parseBlock start, end {
	block := ""
	i := 1
	while [ i > 0 ] do
		pull char
		i ++ if [ char = start ]
		i -- if [ char = end ]
		block .= char if [ i > 0 ]
	done
	return block
}

/** Commands **/

record BlockCommand(code) {
	function evaluate env {
		STACK += env.toRepr()
		env.subenv env, env2
		parse code | _.evaluate env2
		env.push env2
		del STACK[-1]
	}
}

record StringCommand(str) {
	function evaluate env {
		env.push new StrValue(str)
	}
}

record CallCommand(char) {
	function evaluate env {
		STACK += char .." of "..env.toRepr()
		if env.has char do
			(env.get(char)).eval env
		else
			printStack()
			error "Unknown command: `"..char.."'\n"
		done
		del STACK[-1]
	}
}

/** Queue **/

record Queue {
	items : list<<Value>> = new list<<Value>>
	
	function push val... {
		val if [ #val > 0 ] else identity | self.items += _
	}
	
	function pull {
		self.peek
		del self.items[0]
	}
	
	function pullAll {
		self.pull while [ #self.items > 0 ]
	}
	
	function peek {
		if [ #self.items = 0 ] do
			printStack
			error "queue underflow ("..self..")"
			return NIL
		done
		push self.items[0]
	}
	
	function toString {
		return "(" .. [item.toRepr() for item in self.items]&"," .. ")"
	}
}
